buildscript {
  repositories { 
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
    jcenter()
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE'
    classpath 'gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0'
    classpath 'io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE'
    classpath 'ro.isdc.wro4j.gradle:wro4j-gradle-plugin:1.8.0.Beta4'
  }
}

allprojects  {
  apply plugin: 'maven'

  group = 'io.springbox'
  version = '0.0.1-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'com.palantir.docker'
  
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  docker {
    name "jkwong/${project.name}:latest"
    copySpec.into("/")
      .include("**/${project.name}-${project.version}.jar")
      .from("build/libs") 
      .rename("${project.name}-${project.version}.jar", "app.jar")
    files "app.jar"
  }


  task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts.archives packageSources
  
  repositories {
    maven {
      url 'https://repo.spring.io/libs-release/'
    }

    mavenCentral()
    jcenter()
  }
  
  dependencyManagement {
    imports {
	  mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:Dalston.SR3'
    }
   
  }
   
  dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-config'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-bus-amqp'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
      exclude(module: 'commons-logging')
    }
  }
  
}
