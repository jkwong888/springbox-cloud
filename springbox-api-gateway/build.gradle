apply plugin: 'java'
apply plugin: 'wro4j'

description = 'springbox-api-gateway'
dependencies {
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-zuul'
    compile(group: 'org.springframework.cloud', name: 'spring-cloud-starter-eureka') {
        exclude(module: 'guice-grapher')
        exclude(module: 'guice-assistedinject')
        exclude(module: 'jsr166y')
        exclude(module: 'xbean-finder')
        exclude(module: 'asm-all')
        exclude(module: 'guice-multibindings')
        exclude(module: 'xbean-bundleutils')
        exclude(module: 'commons-math')
        exclude(module: 'netflix-infix')
        exclude(module: 'joda-time')
        exclude(module: 'commons-jxpath')
        exclude(module: 'antlr-runtime')
        exclude(module: 'stringtemplate')
        exclude(module: 'antlr')
        exclude(module: 'gson')
        exclude(module: 'annotations')
        exclude(module: 'dagger')
        exclude(module: 'blitz4j')
        exclude(module: 'hibernate-validator')
	}
	compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-ribbon'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-bus-amqp'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-security'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-oauth2'
    compile group: 'org.springframework.security.oauth', name: 'spring-security-oauth2'
    
    webjars 'org.webjars.bower:angular:1.3.8'
    webjars 'org.webjars.bower:angular-route:1.3.8'
    webjars 'org.webjars.bower:angular-cookies:1.3.8'
    webjars 'org.webjars:jquery:2.1.1'
    webjars 'org.webjars:bootstrap:3.2.0'
}

webResources {
    bundle ('angular-bootstrap') {
        js 'webjars/jquery/2.1.1/jquery.min.js'
        js 'webjars/angular/1.3.8/angular.min.js'
        js 'webjars/angular-route/1.3.8/angular-route.min.js'
        js 'webjars/angular-cookies/1.3.8/angular-cookies.min.js'
        postProcessor 'less4j','jsMin'
    }

    bundle ('angular-bootstrap') {
        preProcessor 'lessCssImport'
        css 'webjars/bootstrap/3.2.0/less/bootstrap.less'
    }
    
}

task moveJSResources(type: Copy) {
  eachFile { println it.file }
  from "$project.buildDir/resources/main/static"
  include '*.js'
  into "$project.buildDir/resources/main/static/js/"
}

task moveCSSResources(type: Copy) {
  eachFile { println it.file }
  from "$project.buildDir/resources/main/static"
  include "*.css"
  into "$project.buildDir/resources/main/static/css/"
}

task copyStaticResources(type: Copy) {
  from "src/main/resources/static"
  include "**"
  into "$project.buildDir/resources/main/static/"

}

classes.dependsOn copyStaticResources, moveJSResources, moveCSSResources
copyStaticResources.dependsOn processWebResources
moveJSResources.dependsOn processWebResources
moveCSSResources.dependsOn processWebResources
